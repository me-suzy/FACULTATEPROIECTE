CREATE OR REPLACE TRIGGER trg_pontaje_after_row
	AFTER INSERT OR UPDATE OR DELETE ON pontaje
	FOR EACH ROW
DECLARE
BEGIN
	un_pachet.v_trg_pontaje := TRUE ;
	-- inserare
	IF INSERTING OR UPDATING THEN
		IF un_pachet.f_este_SPSAL('SPORURI', :NEW.marca,
			EXTRACT (YEAR FROM :NEW.data), 
				EXTRACT (MONTH FROM :NEW.data))=FALSE THEN
			-- nu exista in SPORURI; trebuie inserat !
			INSERT INTO sporuri (marca, an, luna, spvech, 
				orenoapte) 
				VALUES (:NEW.marca,
				EXTRACT (YEAR FROM :NEW.data), 
				EXTRACT (MONTH FROM :NEW.data),
				(:NEW.orelucrate * un_pachet.f_salorar (
					:NEW.marca) + :NEW.oreco * 
					un_pachet.f_salorarco (:NEW.marca))
				* un_pachet.f_procsv2004 (
		un_pachet.f_aniv (:NEW.marca, EXTRACT (YEAR FROM :NEW.data), 
 				 EXTRACT (MONTH FROM :NEW.data) 		
					) ),
				:NEW.orenoapte ) ;
		ELSE
			-- nu e primul pontaj pe aceasta luna !
			UPDATE sporuri 
			SET spvech = spvech + 
		(:NEW.orelucrate * un_pachet.f_salorar (
		:NEW.marca) + :NEW.oreco * 
		un_pachet.f_salorarco (:NEW.marca))
		* un_pachet.f_procsv2004 (
		un_pachet.f_aniv (:NEW.marca, EXTRACT (YEAR FROM :NEW.data), 
 				 EXTRACT (MONTH FROM :NEW.data) 		
					) ),
				orenoapte = orenoapte + :NEW.orenoapte
			WHERE marca=:NEW.marca AND 
				an=EXTRACT (YEAR FROM :NEW.data) AND
				luna=EXTRACT (MONTH FROM :NEW.data) ;
		END IF ;

		-- analog, pentru SALARII	
		IF un_pachet.f_este_SPSAL('SALARII', :NEW.marca,
			EXTRACT (YEAR FROM :NEW.data), 
				EXTRACT (MONTH FROM :NEW.data))=FALSE THEN
			-- nu exista in SALARII; trebuie inserat !
			INSERT INTO salarii (marca, an, luna, orelucrate, 
				oreco, venitbaza) 
				VALUES (:NEW.marca,
				EXTRACT (YEAR FROM :NEW.data), 
				EXTRACT (MONTH FROM :NEW.data),
				:NEW.orelucrate, :NEW.oreco,
				(:NEW.orelucrate * un_pachet.f_salorar (
					:NEW.marca) + :NEW.oreco * 
					un_pachet.f_salorarco (:NEW.marca))
				 ) ;
		ELSE
			-- nu e primul pontaj pe aceasta luna !
			UPDATE salarii 
			SET orelucrate = orelucrate + :NEW.orelucrate,
				oreCO = oreCO + :NEW.oreCO,
				venitbaza = venitbaza + 
				(:NEW.orelucrate * un_pachet.f_salorar (
				:NEW.marca) + :NEW.oreco * 
				un_pachet.f_salorarco (:NEW.marca))
			WHERE marca=:NEW.marca AND 
				an=EXTRACT (YEAR FROM :NEW.data) AND
				luna=EXTRACT (MONTH FROM :NEW.data) ;
		END IF ;
	END IF ;

	-- stergere
	IF DELETING OR UPDATING THEN
		UPDATE sporuri 
			SET spvech = spvech - 
		(:OLD.orelucrate * un_pachet.f_salorar (
		:OLD.marca) + :OLD.oreco * 
		un_pachet.f_salorarco (:OLD.marca))
		* un_pachet.f_procsv2004 (
		un_pachet.f_aniv (:OLD.marca, EXTRACT (YEAR FROM :OLD.data), 
 				 EXTRACT (MONTH FROM :OLD.data) 		
					) ),
				orenoapte = orenoapte - :OLD.orenoapte
			WHERE marca=:OLD.marca AND 
				an=EXTRACT (YEAR FROM :OLD.data) AND
				luna=EXTRACT (MONTH FROM :OLD.data) ;

		-- analog, pentru SALARII	
		UPDATE salarii 
		SET orelucrate = orelucrate - :OLD.orelucrate,
			oreCO = oreCO - :OLD.oreCO,
			venitbaza = venitbaza - 
			(:OLD.orelucrate * un_pachet.f_salorar (
			:OLD.marca) + :OLD.oreco * 
			un_pachet.f_salorarco (:OLD.marca))
		WHERE marca=:OLD.marca AND an=EXTRACT (YEAR FROM :OLD.data) AND
				luna=EXTRACT (MONTH FROM :OLD.data) ;
	END IF ;

	un_pachet.v_trg_pontaje := FALSE ;
END;